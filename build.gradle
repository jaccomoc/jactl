/*
 * Copyright 2022 James Crawford
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'signing'
}

group   'io.jactl'
version '2.2.1-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    test {
        resources {
            srcDirs = ["src/test/resources", "src/main/tools"]
        }
    }
}

dependencies {
    implementation 'org.ow2.asm:asm:9.7.1'
    implementation 'org.ow2.asm:asm-util:9.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

tasks.withType(JavaCompile) { options.deprecation = true }

java {

    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

test { useJUnitPlatform() }

jar {
    duplicatesStrategy 'exclude'
    manifest {
        attributes "Main-Class": "io.jactl.Jactl"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    relocate 'org.objectweb.asm', 'io.jactl.ow2.asm'
    archiveClassifier.set('')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Jactl'
                description = 'A secure non-blocking scripting language for the JVM platform.'
                url = 'https://jactl.io'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jaccomoc'
                        name = 'James Crawford'
                        url = 'https://github.com/jaccomoc'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/jaccomoc/jactl.git"
                    developerConnection = "scm:git:git://github.com/jaccomoc/jactl.git"
                    url = 'https://github.com/jaccomoc/jactl'
                }
            }
            pom.withXml {
                asNode().get('dependencies').replaceNode()
            }
        }
    }
//    repositories {
//        maven {
//            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
//            credentials(PasswordCredentials) {
//                username = ossrhUsername
//                password = ossrhPassword
//            }
//        }
//    }
}

//signing {
//    sign publishing.publications.mavenJava
//}
